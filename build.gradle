plugins {
    id 'java'
    id 'dev.architectury.loom' version '1.13-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'org.jetbrains.kotlin.jvm' version '2.2.21' apply false
}

architectury {
    minecraft = minecraft_version
}

allprojects {
    group = maven_group
    version = mod_version
}

// Configure all subprojects
subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    repositories {
        mavenCentral()
        maven { url = "https://maven.fabricmc.net/" }
        maven { url = "https://maven.neoforged.net/releases" }
        maven { url = "https://maven.impactdev.net/repository/development/" }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
        maven { url = "https://maven.shedaniel.me/" }
        maven { url = "https://libraries.minecraft.net" }
        maven { url = "https://maven.parchmentmc.org" }
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
            content {
                includeGroup "maven.modrinth"
            }
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings(loom.layered {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-1.21:${rootProject.parchment_version}@zip")
        })
        
        // Cobblemon
        modImplementation("com.cobblemon:mod:${rootProject.cobblemon_version}")
        
        // Cloth Config
        modApi("me.shedaniel.cloth:cloth-config:${rootProject.cloth_config_version}")
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 21
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}