plugins {
    id "dev.architectury.loom" version "${loom_version}" apply false
    id "architectury-plugin" version "${architectury_plugin_version}"
    id "org.jetbrains.kotlin.jvm" version "${kotlin_jvm_version}"
}

architectury {
    minecraft = minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:${minecraft_version}"

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:${parchment_version}")
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"
    apply plugin: "org.jetbrains.kotlin.jvm"

    base {
        // e.g `chiselmon-fabric`
        archivesName = "${mod_id}-${project.name}"
    }

    version = mod_version
    group = maven_group

    repositories {
        maven { url = "https://maven.neoforged.net/releases" }
        maven { url = 'https://thedarkcolour.github.io/KotlinForForge/' }
        maven { url = "https://maven.parchmentmc.org" }
        maven { url = "https://maven.isxander.dev/releases" }
        maven { url = "https://maven.terraformersmc.com/" }
        maven { url = "https://maven.quiltmc.org/repository/release" }
        maven {
            url = "https://maven.impactdev.net/repository/development"
            content { includeGroup 'com.cobblemon' }
        }
        exclusiveContent {
            forRepository { maven { url = "https://api.modrinth.com/maven" } }
            filter { includeGroup "maven.modrinth" }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
        it.options.encoding = "UTF-8"
    }

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.ow2.asm') {
                details.useVersion '9.8'
                details.because 'NeoForge 1.21.1 requires ASM 9.8 for proper Mixin transformation'
            }
        }
    }
}