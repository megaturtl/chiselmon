plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "2.2.21"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Modrinth's Maven API
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
    maven { url = "https://maven.impactdev.net/repository/development/" }
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://libraries.minecraft.net" }
    maven { url "https://maven.parchmentmc.org" }
}

loom {
    mods {
        "chiselmon" {
            sourceSet sourceSets.main
        }
    }
    
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            source = sourceSets.main
            runDir("run")
        }
    }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	// what cobblemon uses for mappings :shrug:
    mappings(loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.21:${project.parchment_version}")
    })

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "com.cobblemon:fabric:${project.cobblemon_version}+${project.minecraft_version}"
	modImplementation "maven.modrinth:modmenu:${project.mod_menu_version}"
    modImplementation "maven.modrinth:jade:${project.jade_version}+fabric"

    modLocalRuntime "maven.modrinth:sodium:mc1.21.1-0.6.13-fabric"
    modLocalRuntime "net.fabricmc:fabric-language-kotlin:1.13.7+kotlin.2.2.21"



	// Cloth Config
	modApi("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.1"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.10.1"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	// withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
