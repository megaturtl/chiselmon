plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

base {
    archivesName = "${rootProject.archives_base_name}-fabric"
}

repositories {
    maven {
        name = "Terraformers (Mod Menu)"
        url = "https://maven.terraformersmc.com/"
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    
    // Cobblemon
    modImplementation("com.cobblemon:fabric:${rootProject.cobblemon_version}+${rootProject.minecraft_version}")
    
    // Cloth Config
    modApi("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_fabric_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    
    // Mod Menu
    modImplementation "maven.modrinth:modmenu:${rootProject.mod_menu_version}"
    
    // Jade
    modImplementation "maven.modrinth:jade:${rootProject.jade_version}+fabric"
    
    // Runtime only
    modLocalRuntime "maven.modrinth:sodium:mc1.21.1-0.6.13-fabric"
    modLocalRuntime "net.fabricmc:fabric-language-kotlin:${rootProject.fabric_kotlin_version}"

    // Common module
    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.1"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.10.1"
}

processResources {
    def templateProps = [
        "version": project.version,
        "mod_id": rootProject.mod_id,
        "mod_name": rootProject.mod_name,
        "mod_author": rootProject.mod_author,
        "minecraft_version": rootProject.minecraft_version,
        "fabric_loader_version": rootProject.fabric_loader_version,
        "fabric_api_version": rootProject.fabric_api_version,
        "cobblemon_version": rootProject.cobblemon_version
    ]
    
    inputs.properties templateProps

    filesMatching("fabric.mod.json") {
        expand templateProps
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}