plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }

    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation("net.fabricmc:fabric-loader:${fabric_loader_version}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}")
    modImplementation("net.fabricmc:fabric-language-kotlin:${fabric_kotlin_version}")
    modImplementation("com.terraformersmc:modmenu:${mod_menu_version}")

    // Common platform specific deps
    modImplementation("com.cobblemon:fabric:${cobblemon_version}+${minecraft_version}") { transitive = false }
    modImplementation("dev.isxander:yet-another-config-lib:${yacl_version}-fabric")

    implementation include("com.h2database:h2:${h2_version}")

    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowBundle(project(path: ":common", configuration: "transformProductionFabric")) { transitive = false }
}

processResources {
    inputs.property "mod_id", project.property("mod_id")
    inputs.property "mod_display_name", project.property("mod_display_name")
    inputs.property "mod_version", project.property("mod_version")
    inputs.property "mod_author", project.property("mod_author")

    filesMatching("fabric.mod.json") {
        expand(
                mod_id: project.property("mod_id"),
                mod_display_name: project.property("mod_display_name"),
                mod_version: project.property("mod_version"),
                mod_author: project.property("mod_author")
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    getInputFile().set shadowJar.archiveFile
    dependsOn shadowJar
}
