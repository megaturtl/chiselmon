plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }

    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

dependencies {
    neoForge("net.neoforged:neoforge:${neoforge_version}")
    implementation("thedarkcolour:kotlinforforge-neoforge:${kff_version}")

    // Common platform specific deps
    modImplementation("com.cobblemon:neoforge:${cobblemon_version}+${minecraft_version}") { transitive = false }

    implementation include("com.h2database:h2:${h2_version}")
    forgeRuntimeLibrary("com.h2database:h2:${h2_version}")

    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowBundle(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive = false }
}

processResources {
    inputs.property "mod_id", project.property("mod_id")
    inputs.property "mod_display_name", project.property("mod_display_name")
    inputs.property "mod_version", project.property("mod_version")
    inputs.property "mod_author", project.property("mod_author")

    filesMatching("META-INF/mods.toml") {
        expand(
                mod_id: project.property("mod_id"),
                mod_display_name: project.property("mod_display_name"),
                mod_version: project.property("mod_version"),
                mod_author: project.property("mod_author")
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = "dev-shadow"
}

remapJar {
    getInputFile().set shadowJar.archiveFile
    dependsOn shadowJar
}