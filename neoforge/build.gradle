plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

base {
    archivesName = "${rootProject.archives_base_name}-neoforge"
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"
    
    // Common module
    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
        minecraft_version: rootProject.minecraft_version,
        mod_version: project.version,
        mod_id: rootProject.mod_id,
        mod_name: rootProject.mod_name,
        mod_author: rootProject.mod_author,
        neoforge_version: rootProject.neoforge_version,
        cobblemon_version: rootProject.cobblemon_version
    ]
    
    inputs.properties replaceProperties
    
    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}